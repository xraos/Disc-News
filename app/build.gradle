// Calculo del version code
def BUILD_DATE_TIME = new Date().format("yywwuHHmm").toInteger()
logger.lifecycle("Using Buildtime: ${BUILD_DATE_TIME}")

// Nombre del build
//
def BUILD_NAME = "Monkey King"

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "cl.ucn.disc.dam.discnews"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode BUILD_DATE_TIME
        versionName "${BUILD_NAME}-${BUILD_DATE_TIME}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled false
            crunchPngs false
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LGPL2.1'
        exclude 'META-INF/LICENCE'
        exclude 'META-INF/LICENCE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
        //disable 'inlinedApi','AllowBackup'
        showAll true
        htmlReport true
    }
}
// File output name
android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName = "dnews-${variant.versionCode}-${variant.name}.apk"
    }
}

dependencies {
    // implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Simple Logging Facade 4 Java
    implementation 'org.slf4j:slf4j-api:1.7.25'

    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
    testImplementation 'junit:junit:4.12'
    // androidTestImplementation 'com.android.support.test:runner:1.0.1'
    // androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.8.0'

    testAnnotationProcessor 'org.projectlombok:lombok:1.16.18'
    testImplementation 'org.projectlombok:lombok:1.16.18'

    // Logback console core module
    testImplementation 'ch.qos.logback:logback-core:1.2.3'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    // Logback Android core module
    runtimeOnly 'com.github.tony19:logback-android-core:1.1.1-6'
    runtimeOnly('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // Commons Lang3
    implementation 'org.apache.commons:commons-lang3:3.7'

    // Lombok Android
    annotationProcessor 'org.projectlombok:lombok:1.16.18'
    compileOnly 'org.projectlombok:lombok:1.16.18'

    // GSON to JSON converter
    implementation 'com.google.code.gson:gson:2.8.2'

    // Cliente http
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'

    // Prettytime
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    implementation 'joda-time:joda-time:2.9.9'

    // Process image
    implementation 'com.facebook.fresco:fresco:1.5.0'
    // implementation 'com.android.support:support-core-utils:27.0.2'
    implementation 'com.github.stfalcon:frescoimageviewer:0.5.0'

    // DBFLow
    annotationProcessor 'com.github.Raizlabs.DBFlow:dbflow-processor:4.1.2'
    implementation 'com.github.Raizlabs.DBFlow:dbflow-core:4.1.2'
    implementation 'com.github.Raizlabs.DBFlow:dbflow:4.1.2'

    // Device-Year
    implementation 'com.facebook.device.yearclass:yearclass:2.0.0'

    // https://github.com/square/seismic
    implementation 'com.squareup:seismic:1.0.2'

}
